title: The LEP checks upon our progress in accomplishing the coalition's goals...
view-if: (in_popular_front or in_left_front) and (lep_ultimatum_timer = 0 or (welfare_goal_completed and agriculture_goal_completed and economy_goal_completed and finance_goal_completed and labor_goal_completed and reichswehr_goal_completed)) and lep_goals_seen = 1 and lep_ultimatum_seen = 0 and snd_in_government
on-arrival: {!
Q.lep_ultimatum_seen = 1;
Q.lep_goals_completed = 1;
Q.lep_score = 6;
if (Q.welfare_goal && !Q.welfare_goal_completed) {
    Q.lep_goals_completed = 0;
    Q.lep_score -= 1;
}
if (Q.agriculture_goal && !Q.agriculture_goal_completed) {
    Q.lep_goals_completed = 0;
    Q.lep_score -= 1;
}
if (Q.economy_goal && !Q.economy_goal_completed) {
    Q.lep_goals_completed = 0;
    Q.lep_score -= 1;
}
if (Q.finance_goal && !Q.finance_goal_completed) {
    Q.lep_goals_completed = 0;
    Q.lep_score -= 1;
}
if (Q.labor_goal && !Q.labor_goal_completed) {
    Q.lep_goals_completed = 0;
    Q.lep_score -= 1;
}
if (Q.foreign_goal && !Q.foreign_goal_completed) {
    Q.lep_goals_completed = 0;
    Q.lep_score -= 1;
}
if (Q.interior_goal && !Q.interior_goal_completed) {
    Q.lep_goals_completed = 0;
}
if (Q.reichswehr_goal && !Q.reichswehr_goal_completed) {
    Q.lep_goals_completed = 0;
    Q.lep_score -= 1;
}
!}
new-page: true
tags: event
go-to: goals_success if lep_goals_completed; goals_failed if not lep_goals_completed

= LEP Goals

At the start of this government, the LEP presented a list of goals for the coalition:

[? if welfare_goal = 1 : - Expand social welfare programs - [? if welfare_goal_completed : success ?][? if not welfare_goal_completed: failed ?] ?]

[? if agriculture_goal = 1 : - Enact a land reform program. - [? if agriculture_goal_completed : success ?][? if not agriculture_goal_completed: failed ?] ?]

[? if economy_goal = 1 : - Start substantial nationalization of the economy. - [? if economy_goal_completed : success ?][? if not economy_goal_completed: failed ?] ?]

[? if finance_goal = 1 : - Make taxes more progressive. - [? if finance_goal_completed : success ?][? if not finance_goal_completed: failed ?] ?]

[? if labor_goal = 1 : - Support workers in labor disputes. - [? if labor_goal_completed : success ?][? if not labor_goal_completed: failed ?] ?]

[? if foreign_goal = 1 : - Improve our relations with the Soviet Union. - [? if foreign_goal_completed : success ?][? if not foreign_goal_completed: failed ?] ?]

[? if reichswehr_goal = 1 : - Reduce funding for the Reichswehr. - [? if reichswehr_goal_completed : success ?][? if not reichswehr_goal_completed: failed ?] ?]

@goals_failed
on-arrival: lep_relation -= 10; workers_snd -= 8; unemployed_snd -= 8;

Unfortunately, we have failed to accomplish all of the goals the LEP set out. They plan to call a vote of no confidence against our government, and have already begun mounting a propaganda campaign to delegitimize our government in the eyes of the workers.

- @appeal: Appeal to the Communists to have patience - we are trying!
- @kpd_vote_of_no_confidence.support_center: Could we form a new government based on the Center Party and [+ re_name +] (and possibly DVP)?
- @kpd_vote_of_no_confidence.let_it_happen: Let the vote happen.
- @no_vonc: They cannot call a vote of no confidence due to the constitutional reforms, but they can obstruct our government at every opportunity.

@appeal
choose-if: (lep_score >= 4 and lep_relation > 50 and lep_appeal_seen = 0) or lep_party_leader = "Conciliators"
unavailable-subtitle: [? if lep_score < 4: They do not see us as serious in trying to achieve the goals of the coalition. ?] [? if lep_relation < 50: Our relations are too poor. ?] [? if lep_appeal_seen = 1: we do not have any more time. ?]
on-arrival: lep_ultimatum_seen = 0; lep_ultimatum_timer += 6; lep_appeal_seen = 1

We argue that we are working hard to achieve the coalition's goals, and in the name of proletarian solidarity, we must make this coalition function. Surprisingly, the LEP hears our pleas, and is willing to give us six more months before the coalition goals must be achieved.


@goals_success
on-arrival: workers_snd += 8; unemployed_snd += 8; lep_relation += 10; workers_lep += 5; lep_coalition_success += 1
go-to: popular_front_achievement if in_popular_front; united_front_achievement if in_left_front

Against all expectations, the LEP's goals have all been met! Apparently the Communists did not prepare for this eventuality, a worker's government successfully built through democratic means. They will continue to "tolerate" our government for the rest of our term, while still acting as a watchdog against bourgeois backsliding.


@popular_front_achievement
on-arrival: popular_front_success += 1
achievement: volksfront_2

@united_front_achievement
on-arrival: united_front_success += 1
achievement: einheitsfront_2

@no_vonc
view-if: constructive_vonc
on-arrival: in_popular_front = 0; in_left_front = 0; in_minority_government = 1;

We have been effectively made a minority government, and the LEP intends to obstruct our attempts to pass policy.
